;>65c12cpu01.s
;
; BeebIt - BBC Micro Model B Emulator
;
; Opcode routines based on cycle-level CPU emulation
;
; (C) Copyright Crispian Daniels, 2025
;
; Email: <convertedgames@3insdale.me.uk>
;

  GET h.6502zmaps

  GET h.6502cmaps

  GET h.6502cpus

; Use the GET directive to include register definitions as if typed here

  GET h.RegNames

; Area name C$$code advisable as wanted to link with C output

  AREA |C$$code|, CODE, READONLY

; Import global symbols

  IMPORT |r6502opcodeinterruptfetchreturn|
  IMPORT |s2_r6502implied_p|
  IMPORT |s1_r6502implied_p|
  IMPORT |s_r6502implied_p|
  IMPORT |r6502nobranch|
  IMPORT |r65c12branch|
  IMPORT |s2_r6502interruptfetch_p|
  IMPORT |s1_r6502interruptfetch_p|
  IMPORT |s_r6502interruptfetch_p|

; Export global symbols

  EXPORT |r65c12brkroutine|
  EXPORT |r65c12interruptroutine|
  EXPORT |r65c12bplroutine|
  EXPORT |r65c12incaroutine|

  MACRO
  BRK_ACTION
  ADD r7,r7,#2:SHL:16
  LDR r4,[r0,#:INDEX:R6502_SP_CODE]
  MOV r2,r7,LSR #24
  LDR r5,[r0,#:INDEX:R6502_PAGEONE]
  SUB r3,r4,#1:SHL:24
  STRB r2,[r5,r4,LSR #24]
  MOV r2,r7,LSR #16
  SUB r4,r3,#1:SHL:24
  STRB r2,[r5,r3,LSR #24]
  LDR r3,[r0,#:INDEX:R6502_INTERRUPTQUEUE]
  TST r3,#(R6502_IRQ_ENABLE<<12)
  BICNE r2,r8,#IFLAG
  MOVEQ r2,r8
  BIC r8,r8,#DFLAG
  STRB r2,[r5,r4,LSR #24]
  SUB r4,r4,#1:SHL:24
  STR r4,[r0,#:INDEX:R6502_SP_CODE]
  BIC r3,r3,#(R6502_IRQ_ENABLE<<4)|R6502_IRQ_ENABLE
  LDR r5,[r0,#:INDEX:R6502_PAGEMINUSONE]
  BIC r3,r3,#(R6502_IRQ_ENABLE<<8)
  LDR r1,[r5,#&FC]
  MOV r7,#ADDRESSING_BASEFLAG
  MOV r1,r1,LSR #16
  STR r3,[r0,#:INDEX:R6502_INTERRUPTQUEUE]
  ORR r6,r7,r1,LSL #16
  ORR r7,r7,r1,LSL #16
  MEND

  MACRO
  INTERRUPT_ACTION
  LDR r4,[r0,#:INDEX:R6502_SP_CODE]
  MOV r2,r7,LSR #24
  LDR r5,[r0,#:INDEX:R6502_PAGEONE]
  SUB r3,r4,#1:SHL:24
  STRB r2,[r5,r4,LSR #24]
  MOV r2,r7,LSR #16
  SUB r4,r3,#1:SHL:24
  STRB r2,[r5,r3,LSR #24]
  LDR r3,[r0,#:INDEX:R6502_INTERRUPTQUEUE]
  TST r3,#(R6502_IRQ_ENABLE<<12)
  BICNE r2,r8,#BFLAG|IFLAG
  BICEQ r2,r8,#BFLAG
  BIC r8,r8,#DFLAG
  TST r3,#(R6502_NMI_ENABLE<<12)
  STRB r2,[r5,r4,LSR #24]
  SUB r4,r4,#1:SHL:24
  TSTNE r3,#(R6502_NMI<<12)
  STR r4,[r0,#:INDEX:R6502_SP_CODE]
  BICNE r3,r3,#(R6502_NMI_IRQ_ENABLE<<4)|R6502_NMI_IRQ_ENABLE
  BICEQ r3,r3,#(R6502_IRQ_ENABLE<<4)|R6502_IRQ_ENABLE
  LDR r5,[r0,#:INDEX:R6502_PAGEMINUSONE]
  BICNE r3,r3,#(R6502_NMI_IRQ_ENABLE<<8)
  BICEQ r3,r3,#(R6502_IRQ_ENABLE<<8)
  ANDNE r2,r3,#(R6502_NMI<<8)|(R6502_NMI<<4)
  LDRNE r1,[r5,#&F8]
  LDREQ r1,[r5,#&FC]
  EORNE r2,r2,#(R6502_NMI<<8)|(R6502_NMI<<4)
  MOV r7,#ADDRESSING_BASEFLAG
  ORRNE r3,r3,r2,LSR #6
  MOV r1,r1,LSR #16
  STR r3,[r0,#:INDEX:R6502_INTERRUPTQUEUE]
  ORR r6,r7,r1,LSL #16
  ORR r7,r7,r1,LSL #16
  MEND

  MACRO
  INCA_ACTION
  LDR r2,[r0,#:INDEX:R6502_A_SHIFT]
  BIC r8,r8,#NFLAG|ZFLAG
  ADDS r2,r2,#1:SHL:24
  ORRMI r8,r8,#NFLAG
  STR r2,[r0,#:INDEX:R6502_A_SHIFT]
  ORREQ r8,r8,#ZFLAG
  MEND

|r65c12brkroutine|
  R6502_OPCODE_PUSH
  R6502_OPERAND_READ_BRANCH r65c12brk_p1
r65c12brk_rs1
  R6502_OPCODE_CYCLES 6,0
  BRK_ACTION
  R6502_READ_BRANCH r6502interruptfetch_p
  LDRB r4,[r1]
  R6502_OPCODE_CYCLES 1,0
  B r6502opcodeinterruptfetchreturn
s2_r65c12brk_p1
  R6502_OPCODE_CYCLES 0,2
  B s_r65c12brk_p1
s1_r65c12brk_p1
  R6502_OPCODE_CYCLES 0,1
s_r65c12brk_p1
  R6502_OPCODE_READ_SPECIAL
  B r65c12brk_rs1

|r65c12interruptroutine|
  R6502_OPCODE_PUSH
  R6502_NO_OPERAND_READ_BRANCH r65c12interrupt_p1
r65c12interrupt_rs1
  R6502_OPCODE_CYCLES 6,0
  INTERRUPT_ACTION
  R6502_READ_BRANCH r6502interruptfetch_p
  LDRB r4,[r1]
  R6502_OPCODE_CYCLES 1,0
  B r6502opcodeinterruptfetchreturn
s2_r65c12interrupt_p1
  R6502_OPCODE_CYCLES 0,2
  B s_r65c12interrupt_p1
s1_r65c12interrupt_p1
  R6502_OPCODE_CYCLES 0,1
s_r65c12interrupt_p1
  R6502_OPCODE_READ_SPECIAL
  B r65c12interrupt_rs1

|r65c12bplroutine|
  R6502_OPCODE_PUSH
  R65C12_BRANCH_IF_CLEAR_OPCODE_RETURN NFLAG

|r65c12incaroutine|
  R6502_OPCODE_PUSH
  INCA_ACTION
  R6502_IMPLIED_OPCODE_RETURN


;Data Area

;  AREA    |C$$data|, DATA

  END
