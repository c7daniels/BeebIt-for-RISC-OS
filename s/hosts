;>hosts.s
;
; BeebIt - BBC Micro Model B Emulator
;
; (C) Copyright Michael J Foot, 1998-2024
;
; Email: <mjfoot.nz@gmail.com>
;
; Fast ROM paging contributed by Crispian Daniels in 2024
;
; Email: <convertedgames@3insdale.me.uk>
;

  GET h.6502zmaps

  GET h.6502cmaps

  GET h.6502cpus

  GET h.hostmaps

  GET h.hosts

; Use the GET directive to include register definitions as if typed here

  GET h.RegNames

; Use the GET directive to include a list of SWI names as if typed here

  GET h.SWInames

; Area name C$$code advisable as wanted to link with C output

  AREA    |C$$code|, CODE, READONLY

; Import global symbols

  IMPORT |systemviaatzerocycles|
  IMPORT |userviaatzerocycles|
  IMPORT |i8271poll|
  IMPORT |w1770poll|
  IMPORT |adcpoll|
  IMPORT |aciapoll|
  IMPORT |videoatzerocycles|
  IMPORT |keyscanf1p0c|
  IMPORT |keyscanf1p0C|
  IMPORT |keyscanf1p0i|
  IMPORT |keyscanf1p0I|
  IMPORT |keyscanf1|
  IMPORT |keyscanb1|
  IMPORT |keyscanf1p1c|
  IMPORT |keyscanf1p1C|
  IMPORT |keyscanf1p1i|
  IMPORT |keyscanf1p1I|

; Export global symbols

  EXPORT |hostmap|

  EXPORT |host_r6502address0000|
  EXPORT |host_r6502address3000|
  EXPORT |host_r6502addressbp3000_ram|
  EXPORT |host_r6502addressbp3000_ramram|
  EXPORT |host_r6502addressm3000_x|
  EXPORT |host_r6502addressm3000_e|
  EXPORT |host_r6502addressbp8000_ram|
  EXPORT |host_r6502addressm8000_ram|
  EXPORT |host_r6502addressmC000_y|
  EXPORT |host_r6502addressFE00_fast|
  EXPORT |host_r6502addressFE00_slow|

  EXPORT |host_r6502read8000_0|
  EXPORT |host_r6502read8000_1|
  EXPORT |host_r6502read8000_2|
  EXPORT |host_r6502read8000_3|
  EXPORT |host_r6502read8000_4|
  EXPORT |host_r6502read8000_5|
  EXPORT |host_r6502read8000_6|
  EXPORT |host_r6502read8000_7|
  EXPORT |host_r6502read8000_8|
  EXPORT |host_r6502read8000_9|
  EXPORT |host_r6502read8000_A|
  EXPORT |host_r6502read8000_B|
  EXPORT |host_r6502read8000_C|
  EXPORT |host_r6502read8000_D|
  EXPORT |host_r6502read8000_E|
  EXPORT |host_r6502read8000_F|
  EXPORT |host_r6502readC000|
  EXPORT |host_r6502readF000|
  EXPORT |host_r6502readmFC00|
  EXPORT |host_r6502modify8000_0|
  EXPORT |host_r6502modify8000_1|
  EXPORT |host_r6502modify8000_2|
  EXPORT |host_r6502modify8000_3|
  EXPORT |host_r6502modify8000_4|
  EXPORT |host_r6502modify8000_5|
  EXPORT |host_r6502modify8000_6|
  EXPORT |host_r6502modify8000_7|
  EXPORT |host_r6502modify8000_8|
  EXPORT |host_r6502modify8000_9|
  EXPORT |host_r6502modify8000_A|
  EXPORT |host_r6502modify8000_B|
  EXPORT |host_r6502modify8000_C|
  EXPORT |host_r6502modify8000_D|
  EXPORT |host_r6502modify8000_E|
  EXPORT |host_r6502modify8000_F|
  EXPORT |host_r6502modifyC000|
  EXPORT |host_r6502modifyF000|
  EXPORT |host_r6502modifymFC00|

  EXPORT |host_r6502readfred|
  EXPORT |host_r6502readjim|
  EXPORT |host_r6502writefred|
  EXPORT |host_r6502writejim|
  EXPORT |host_r6502readmfred|
  EXPORT |host_r6502readmjim|
  EXPORT |host_r6502writemfred|
  EXPORT |host_r6502writemjim|

  EXPORT |host_r6502cycleroutines|

  EXPORT |host_r6502readspecial|
  EXPORT |host_r6502writespecial|

  EXPORT |hostadvancetimers|
  EXPORT |hostexecute|

  ASSERT :INDEX:HOST_M_SHIFT == :INDEX:R6502ZONE_M_SHIFT
  ASSERT :INDEX:HOST_M == :INDEX:R6502ZONE_M

  ASSERT :INDEX:HOST_AC == :INDEX:R6502ZONE_AC
  ASSERT :INDEX:HOST_BC == :INDEX:R6502ZONE_BC
  ASSERT :INDEX:HOST_CC == :INDEX:R6502ZONE_CC
  ASSERT :INDEX:HOST_DC == :INDEX:R6502ZONE_DC
  ASSERT :INDEX:HOST_EC == :INDEX:R6502ZONE_EC

host_r6502address0000
host_r6502address3000
host_r6502readC000
  ADD r3,r0,#:INDEX:HOST_MEMORY
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502addressbp3000_ram
  ADD r3,r7,#&20000000
  CMP r3,#&E0000000
  ADDLO r3,r0,#:INDEX:HOST_MEMORY
  ADDHS r3,r0,#:INDEX:HOST_SHADOW
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502addressbp3000_ramram
  ADD r3,r7,#&50000000
  CMP r3,#&F0000000
  BHS host_r6502addressbp3000_ramram2
  ADD r3,r3,#&D0000000
  CMP r3,#&E0000000
  ADDLO r3,r0,#:INDEX:HOST_MEMORY
host_r6502addressbp3000_ramram2
  ADDHS r3,r0,#:INDEX:HOST_SHADOW
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502addressm3000_x
  ADD r3,r0,#:INDEX:HOST_SHADOW
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502addressm3000_e
  ADD r3,r7,#&20000000
  CMP r3,#&E0000000
  ADDLO r3,r0,#:INDEX:HOST_MEMORY
  ADDHS r3,r0,#:INDEX:HOST_SHADOW
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502addressbp8000_ram
host_r6502addressm8000_ram
  ADD r3,r0,#:INDEX:HOST_SHADOW-&8000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502addressmC000_y
  ADD r3,r0,#:INDEX:HOST_SHADOW-&B000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502addressFE00_slow
  LDR r12,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ODD]
  LDR r3,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_TOGO]
  EOR r3,r3,r12
  LDR r12,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ORIGINAL]
  EOR r3,r3,r12
  TST r3,#1
  HOST_ADDRESSING_RETURN_STRETCHSPECIAL

host_r6502addressFE00_fast
  HOST_ADDRESSING_RETURN_SPECIAL

host_r6502read8000_0
  ADD r3,r0,#:INDEX:HOST_ROMS-&8000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_1
  ADD r3,r0,#:INDEX:HOST_ROMS-&4000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_2
  ADD r3,r0,#:INDEX:HOST_ROMS
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_3
  ADD r3,r0,#:INDEX:HOST_ROMS+&4000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_4
  ADD r3,r0,#:INDEX:HOST_ROMS+&8000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_5
  ADD r3,r0,#:INDEX:HOST_ROMS+&C000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_6
  ADD r3,r0,#:INDEX:HOST_ROMS+&10000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_7
  ADD r3,r0,#:INDEX:HOST_ROMS+&14000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_8
  ADD r3,r0,#:INDEX:HOST_ROMS+&18000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_9
  ADD r3,r0,#:INDEX:HOST_ROMS+&1C000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_A
  ADD r3,r0,#:INDEX:HOST_ROMS+&20000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_B
  ADD r3,r0,#:INDEX:HOST_ROMS+&24000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_C
  ADD r3,r0,#:INDEX:HOST_ROMS+&28000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_D
  ADD r3,r0,#:INDEX:HOST_ROMS+&2C000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_E
  ADD r3,r0,#:INDEX:HOST_ROMS+&30000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502read8000_F
  ADD r3,r0,#:INDEX:HOST_ROMS+&34000
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502readF000
  CMP r1,#&FC000000
  SUBHS r3,r0,#(&FC00:SHL:2)-:INDEX:HOST_READHIGH
  LDRHS pc,[r3,r1,LSR #14]
  ADD r3,r0,#:INDEX:HOST_MEMORY
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502readmFC00
  LDR pc,[r0,#:INDEX:HOST_READMFC00]

host_r6502modify8000_0
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE]
  ADD r3,r0,#:INDEX:HOST_ROMS-&8000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_1
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+1]
  ADD r3,r0,#:INDEX:HOST_ROMS-&4000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_2
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+2]
  ADD r3,r0,#:INDEX:HOST_ROMS
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_3
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+3]
  ADD r3,r0,#:INDEX:HOST_ROMS+&4000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_4
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+4]
  ADD r3,r0,#:INDEX:HOST_ROMS+&8000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_5
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+5]
  ADD r3,r0,#:INDEX:HOST_ROMS+&C000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_6
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+6]
  ADD r3,r0,#:INDEX:HOST_ROMS+&10000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_7
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+7]
  ADD r3,r0,#:INDEX:HOST_ROMS+&14000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_8
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+8]
  ADD r3,r0,#:INDEX:HOST_ROMS+&18000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_9
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+9]
  ADD r3,r0,#:INDEX:HOST_ROMS+&1C000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_A
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+10]
  ADD r3,r0,#:INDEX:HOST_ROMS+&20000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_B
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+11]
  ADD r3,r0,#:INDEX:HOST_ROMS+&24000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_C
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+12]
  ADD r3,r0,#:INDEX:HOST_ROMS+&28000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_D
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+13]
  ADD r3,r0,#:INDEX:HOST_ROMS+&2C000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_E
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+14]
  ADD r3,r0,#:INDEX:HOST_ROMS+&30000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modify8000_F
  LDRB r12,[r0,#:INDEX:HOST_ROMWRITABLE+15]
  ADD r3,r0,#:INDEX:HOST_ROMS+&34000
  TST r12,r12
  ADD r1,r3,r1,LSR #16
  HOST_ADDRESSING_RETURN_MAPPEDNE
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modifyC000
  ADD r3,r0,#:INDEX:HOST_MEMORY
  ADD r1,r3,r1,LSR #16
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modifyF000
  CMP r1,#&FC000000
  SUBHS r3,r0,#(&FC00:SHL:2)-:INDEX:HOST_MODIFYHIGH
  LDRHS pc,[r3,r1,LSR #14]
  ADD r3,r0,#:INDEX:HOST_MEMORY
  ADD r1,r3,r1,LSR #16
  LDRB r3,[r1]
  ADD r1,r0,#:INDEX:R6502_COPIEDVALUE
  STRB r3,[r0,#:INDEX:R6502_COPIEDVALUE]
  HOST_ADDRESSING_RETURN_MAPPED

host_r6502modifymFC00
  LDR pc,[r0,#:INDEX:HOST_MODIFYMFC00]

host_r6502readmfred
  LDRB r3,[r0,#:INDEX:HOST_ACCCON]
  TST r3,#&60
  MOVEQ r2,#0
  MOVEQ pc,lr
host_r6502readfred
  MOV r2,#0 ; unimplemented
  STR r0,[sp, #-4]!
  MOV r1,r1,LSR #16
  MOV r2,#&FE
  MOV r0,#&92 ; (146) read FRED
  AND r1,r1,#255 ; low 8 bits in R1
  SWI XOS_Byte ; may return V
  LDR r0, [sp], #4
  STRB r2,[r0,#:INDEX:HOST_M]
  MOV pc,lr

host_r6502readmjim
  LDRB r3,[r0,#:INDEX:HOST_ACCCON]
  TST r3,#&60
  MOVEQ r2,#0
  MOVEQ pc,lr
host_r6502readjim
  MOV r2,#0 ; unimplemented
  STR r0,[sp, #-4]!
  MOV r1,r1,LSR #16
  MOV r2,#&FE
  MOV r0,#&94 ; (148) read JIM
  AND r1,r1,#255 ; low 8 bits in R1
  SWI XOS_Byte ; may return V
  LDR r0, [sp], #4
  STRB r2,[r0,#:INDEX:HOST_M]
  MOV pc,lr

host_r6502writemfred
  LDRB r3,[r0,#:INDEX:HOST_ACCCON]
  TST r3,#&60
  MOVEQ pc,lr
host_r6502writefred
  STR r0,[sp, #-4]!
  MOV r1,r1,LSR #16
  MOV r0,#&93 ; (147) write FRED
  AND r1,r1,#255 ; low 8 bits in R1
  SWI XOS_Byte ; may return V
  LDR r0, [sp], #4
  MOV pc,lr

host_r6502writemjim
  LDRB r3,[r0,#:INDEX:HOST_ACCCON]
  TST r3,#&60
  MOVEQ pc,lr
host_r6502writejim
  STR r0,[sp, #-4]!
  MOV r1,r1,LSR #16
  MOV r0,#&95 ; (149) write JIM
  AND r1,r1,#255 ; low 8 bits in R1
  SWI XOS_Byte ; may return V
  LDR r0, [sp], #4
  MOV pc,lr

|host_r6502cycleroutines|
  DCD host_r6502cycle1,host_r6502cycle0s1

  MACRO
  TICK1SCAN1RETURN $shift,$routines
  [ $shift==0
  LDR r3,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_TOGO]
  |
  LDR r3,[r0,#:INDEX:R6502_INTERRUPTQUEUE]
  TST r3,#R6502_NMI
  AND r2,r3,#&0F
  ORREQ r2,r2,#R6502_NMI_ENABLE
  ORR r2,r2,r3,LSL #4
  LDR r3,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_TOGO]
  STR r2,[r0,#:INDEX:R6502_INTERRUPTQUEUE]
  ]
  LDR r1,[r0,#:INDEX:HOST_KEYSCANSTATE]
  SUBS r3,r3,#1
  LDR r2,[r0,#:INDEX:HOST_KEYCOLUMNFLAGS]
  STR r3,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_TOGO]
  STREQ lr,[sp, #-4]!
  ADREQ lr,advancetimersreturn
  MOV r2,r2,ROR r1
  ADR r3,$routines
  ORR r2,r2,r2,LSR #1
  LDR pc,[r3,r1,LSR #26]
  MEND

host_r6502cycle0s1routines
  DCD keyscanf1p0c, keyscanf1p0C, keyscanf1p0c, keyscanf1p0C
  DCD keyscanf1p0c, keyscanf1p0C, keyscanf1p0i, keyscanf1p0I
  DCD keyscanf1, keyscanf1, keyscanb1, keyscanb1
  DCD keyscanf1, keyscanf1, keyscanb1, keyscanb1

host_r6502cycle1routines
  DCD keyscanf1p1c, keyscanf1p1C, keyscanf1p1c, keyscanf1p1C
  DCD keyscanf1p1c, keyscanf1p1C, keyscanf1p1i, keyscanf1p1I
  DCD keyscanf1, keyscanf1, keyscanb1, keyscanb1
  DCD keyscanf1, keyscanf1, keyscanb1, keyscanb1

host_r6502cycle0s1
  TICK1SCAN1RETURN 0,host_r6502cycle0s1routines

host_r6502cycle1
  TICK1SCAN1RETURN 1,host_r6502cycle1routines

advancetimersreturn
  LDR lr, [sp], #4
  B hostadvancetimers

|host_r6502readspecial|
  SUB r3,r0,#(&FC00:SHL:2)-:INDEX:HOST_SPECIALREADOPS
  LDR pc,[r3,r1,LSR #14]

|host_r6502writespecial|
  SUB r3,r0,#(&FC00:SHL:2)-:INDEX:HOST_SPECIALWRITEOPS
  LDR pc,[r3,r1,LSR #14]

|hostadvancetimers|
  STMFD sp!,{r4-r5,lr}

  MOV r1,#0
  LDR r4,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ORIGINAL]
  STR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ORIGINAL]
  TST r4,#1
  LDRNE r2,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ODD]
  EORNE r2,r2,#1
  STRNE r2,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ODD]
  LDR r2,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_MONOTONIC]
  ADD r2,r2,r4
  STR r2,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_MONOTONIC]
  MOV r5,#0x10000

  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_SYSVIATOGO]
  SUBS r1,r1,r4
  BMI advancetimers2
  STR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_SYSVIATOGO]
  ADDNE pc,pc,#8-4
  BL systemviaatzerocycles
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_SYSVIATOGO]
  SUB r2,r1,#1
  CMP r2,r5
  MOVLO r5,r1

advancetimers2
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_USRVIATOGO]
  SUBS r1,r1,r4
  BMI advancetimers3
  STR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_USRVIATOGO]
  ADDNE pc,pc,#8-4
  BL userviaatzerocycles
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_USRVIATOGO]
  SUB r2,r1,#1
  CMP r2,r5
  MOVLO r5,r1

advancetimers3
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_I8271TOGO]
  SUBS r1,r1,r4
  BMI advancetimers4
  STR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_I8271TOGO]
  ADDNE pc,pc,#8-4
  BL i8271poll
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_I8271TOGO]
  SUB r2,r1,#1
  CMP r2,r5
  MOVLO r5,r1

advancetimers4
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_W1770TOGO]
  SUBS r1,r1,r4
  BMI advancetimers5
  STR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_W1770TOGO]
  ADDNE pc,pc,#8-4
  BL w1770poll
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_W1770TOGO]
  SUB r2,r1,#1
  CMP r2,r5
  MOVLO r5,r1

advancetimers5
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ADCTOGO]
  SUBS r1,r1,r4
  BMI advancetimers6
  STR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ADCTOGO]
  ADDNE pc,pc,#8-4
  BL adcpoll
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ADCTOGO]
  SUB r2,r1,#1
  CMP r2,r5
  MOVLO r5,r1

advancetimers6
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_M6850TOGO]
  SUBS r1,r1,r4
  BMI advancetimers7
  STR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_M6850TOGO]
  ADDNE pc,pc,#8-4
  BL aciapoll
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_M6850TOGO]
  SUB r2,r1,#1
  CMP r2,r5
  MOVLO r5,r1

advancetimers7
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_VIDEOTOGO]
  SUBS r1,r1,r4
  BMI advancetimers8
  STR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_VIDEOTOGO]
  ADDNE pc,pc,#8-4
  BL videoatzerocycles
  LDR r1,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_VIDEOTOGO]
  SUB r2,r1,#1
  CMP r2,r5
  MOVLO r5,r1

advancetimers8
  STR r5,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_ORIGINAL]
  STR r5,[r0,#:INDEX:HOST_CYCLES+:INDEX:HOSTCYCLES_TOGO]
  LDMFD sp!,{r4-r5,pc}

|hostexecute|
  LDR r0,=hostmap
  R6502_EXECUTE_PUSH
  LDR r1,[r0,#:INDEX:R6502_EXECUTE_CPU_STATE]
  MOV r3,#R6502_REGULAR_TRANSITION
  STR r1,[r0,#:INDEX:R6502_CPU_STATE]
  STR r3,[r0,#:INDEX:R6502_TRANSITION1]
  STR r3,[r0,#:INDEX:R6502_TRANSITION2]
  [ Optimise6502Links
  ADR r2,hostexecute1
  ADR r3,hostexecute2
  STR r2,[r0,#:INDEX:R6502_CONTINUELINK]
  STR r3,[r0,#:INDEX:R6502_YIELDLINK]
  ]
hostexecute1
  [ Optimise6502Links
  LDR r1,[r0,#:INDEX:R6502_CPU_STATE]
  LDR pc, [r1], #4
  |
  ADD r3,r0,#:INDEX:R6502_SEQUENCES
  LDR r1,[r0,#:INDEX:R6502_CPU_STATE]
  LDR r2,[r3,#R6502_YIELD_SEQUENCE:SHL:2]
  CMP r1,r2
  ADR lr,hostexecute1
  LDRNE pc, [r1], #4
  ]
hostexecute2
  R6502_EXECUTE_POP

;Data Area

  AREA    |C$$bss|, NOINIT

|hostmap|
  % SIZEOF_HOSTMAP

  END
